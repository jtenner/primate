module Session

from "./external.gr" include External
from "json" include Json
from "result" include Result
from "runtime/unsafe/offsets" include Offsets
from "runtime/unsafe/wasmi32" include WasmI32
from "string" include String
from "bytes" include Bytes
from "uint32" include Uint32

use Json.{ type Json }
use Offsets.{ _STR_DATA_OFFSET }
use WasmI32.{ (+) as (+.) }

provide exception InvalidSessionPayload

provide record Session {
  data: Json,
  new: Bool,
  id: String,
}

let parseSessionPayload = (payload: Bytes) => {
  use Bytes.{ getUint32, slice, toString }
  use Uint32.{ toNumber, (<), (+) as (+.) }
  
  let offset = 0
  let strLength = Uint32.toNumber(Bytes.getUint32(0, payload))
  let str = Bytes.slice(4, strLength, payload)
  let data = Result.expect("Invalid JSON data", Json.parse(toString(str)))
  
  let offset = offset + 4 + strLength
  let new = Bytes.getUint32(offset, payload) == 1ul
  let offset = offset + 4
  
  let idLength = Uint32.toNumber(Bytes.getUint32(offset, payload))
  let idBytes = Bytes.slice(offset + 4, idLength, payload)
  let id = toString(idBytes)

  let offset = offset + 4 + idLength
  if (offset != Bytes.length(payload)) throw InvalidSessionPayload

  {
    data,
    new,
    id,
  }: Session
}

@unsafe
provide let make = (data: Json) => {
  let str = Result.expect("Invalid JSON", Json.toString(data))
  let ptr = WasmI32.fromGrain(str) +. _STR_DATA_OFFSET
  let byteLength = WasmI32.fromGrain(String.byteLength(str))
  External.sendPayload(ptr, byteLength)
  ignore(str)
  
  External.newSessionPayload()
  let payload = External.receiveBytes()
  parseSessionPayload(payload)
}

@unsafe
provide let get = () => {
  External.getSessionPayload()
  let payload = External.receiveBytes()
  parseSessionPayload(payload)
}
